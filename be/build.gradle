import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    id 'org.springframework.boot' version '4.0.3' apply false
    id 'org.barfuin.gradle.jacocolog' version '3.1.0'
    id "org.sonarqube" version "7.2.2.6593"
    id 'org.openapi.generator' version '7.19.0' apply false
}

project.ext.group = 'org.theoriok'
project.ext.version = '1.0-SNAPSHOT'
project.ext.name = 'inventory'

ext {
    jacocoVersion = '0.8.14'
    flywayVersion = '12.0.2'
    annotationsVersion = '26.0.2-1'
    checkstyleVersion = "13.2.0"
    pmdVersion = '7.21.0'
    recordBuilderVersion = '52'
}

allprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'

    group = rootProject.ext.group
    version = rootProject.ext.version

    repositories {
        mavenCentral()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_25
        targetCompatibility = JavaVersion.VERSION_25
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    tasks.matching { task -> (task.name == 'jacocoAggregatedReport') }.forEach { task ->
        task.reports {
            xml.required = true
            xml.outputLocation = file("build/reports/jacocoAggregatedReport.xml")
        }
    }

    pluginManager.withPlugin('java-library') {

        dependencies {
            implementation 'org.apache.commons:commons-lang3'
            implementation 'org.slf4j:slf4j-api'

            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            testImplementation 'org.springframework.boot:spring-boot-starter-log4j2'
            testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        }

        javadoc {
            javadoc.options.addStringOption('Xdoclint:none', '-quiet')
        }

        java {
            withJavadocJar()
            withSourcesJar()
        }

        pmd {
            ruleSets = []
            ignoreFailures = false
            toolVersion = pmdVersion
        }

        pmdMain {
            ruleSetFiles = files("${project.rootDir}/config/pmd/pmd.xml")
        }

        pmdTest {
            ruleSetFiles = files("${project.rootDir}/config/pmd/testPmd.xml")
        }

        checkstyle {
            toolVersion = checkstyleVersion
            ignoreFailures = false
            maxErrors = 0
            maxWarnings = 0
            configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        }

        jacoco {
            toolVersion = jacocoVersion
        }

        jacocoTestReport {
            reports {
                xml.required = true
                xml.outputLocation = file("build/reports/jacocoTestReport.xml")
            }
        }
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }

        dependencies {
            dependency "org.flywaydb:flyway-core:$flywayVersion"
            dependency "org.jetbrains:annotations:$annotationsVersion"
        }
    }

    configurations.configureEach {
        exclude group: "ch.qos.logback"

        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
            cacheDynamicVersionsFor 0, 'minutes'

            dependencySubstitution.all { dependency ->
                if (dependency.requested instanceof ModuleComponentSelector && dependency.requested.module == 'spring-boot-starter-logging') {
                    dependency.useTarget("org.springframework.boot:spring-boot-starter-log4j2:$dependency.requested.version", 'Use Log4j2 instead of Logback')
                }
            }
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "theoriok_inventory"
        property "sonar.organization", "theorioksoft"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.rootDir}/build/reports/jacocoAggregatedReport.xml"
        property "sonar.tests", "src/test"
        property "sonar.coverage.exclusions", "**/config/**,**/exception/**,**/persistence/entities/**"
    }
}